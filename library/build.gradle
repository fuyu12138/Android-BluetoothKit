apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'maven-publish'
version = "1.4.0"

android {
    compileSdkVersion 23
    buildToolsVersion '25.0.0'

    resourcePrefix "bluetoothkit_"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}


dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
}

def siteUrl = 'https://github.com/dingjikerbo/BluetoothKit' // 项目的主页
def gitUrl = 'https://github.com/dingjikerbo/BluetoothKit.git' // Git仓库的url
group = "com.inuker.bluetooth" // Maven Group ID for the artifact，一般填你唯一的包名
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                // Add your description here
                name 'Android Bluetooth Library' //项目描述
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'dingjikerbo'    //填写的一些基本信息
                        name 'inuker'
                        email 'dingjikerbo@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

//afterEvaluate {
publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.github.SmartZane'   // GitHub 用户名
            artifactId = 'Android-BluetoothKit' // 项目名称
            version = '1.0'                     // 版本号

            afterEvaluate {
                from(components["release"])
            }
        }
    }
}
//}
